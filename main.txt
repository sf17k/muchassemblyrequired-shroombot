;;  Options
RUN_TESTS equ 1
SKIP_COSTLY_TESTS equ 0

;;                         ,-------------------,
;;                         | Hello, my name is |
;;                         |     shroombot     |
;;                         `-------------------'



;;  Table of Contents  #toc
; 0. START
; 1. BEHAVIOR
; 2. DRIVERS
;    - Movement
;    - Display
;    - Sensors
; 3. TESTING
; 4. MATH
; 5. TYPES
;    - Map
; 6. UTILITY



;;  Tips
; Names prefixed with _underscores are implementation details.

;;  Memory layout
; 0x0000 lidar path
; 0x0050 some execution statistics
; 0x0100 lidar map
; 0x0200 the program (.data and .text)



;;  Constants

FALSE equ 0x0000 ; must be 0x0000
TRUE equ 0xffff ; must be 0xffff

; for testing framework
REG_A equ 0x0001
REG_B equ 0x0002
REG_C equ 0x0004
REG_D equ 0x0008
REG_X equ 0x0010
REG_Y equ 0x0020
REG_BP equ 0x0040
REG_SP equ 0x0080

DIR_N equ 0
DIR_E equ 1
DIR_S equ 2
DIR_W equ 3

HW_LEGS equ 0x0001
HW_LIDAR equ 0x0003
HW_RNG equ 0x0007
HW_CLOCK equ 0x0008
HW_HOLO equ 0x0009
HW_BATTERY equ 0x000a

HOLO_CLEAR equ 0
HOLO_DISPLAY_HEX equ 1
HOLO_DISPLAY_STRING equ 2

BATTERY_GET_CHARGE equ 1
BATTERY_GET_CAPACITY equ 2

LEGS_TURN equ 1 ; 20 kJ
LEGS_WALK equ 2 ; 100 kJ

LIDAR_GET_POS equ 1
LIDAR_GET_PATH equ 2 ; 50 kJ
LIDAR_GET_MAP equ 3 ; 10 kJ
LIDAR_GET_WORLD_POS equ 4

LIDAR_PATH_ADDR equ 0x0000
LIDAR_MAP_ADDR equ 0x0100

MAP_SIZE equ 0x0100 ; 16x16

; used by updateMapPeriodically
;MAP_UPDATE_INTERVAL equ 4 ; ticks
;MAP_UPDATE_INTERVAL_MOVING equ 2 ; ticks, for moving targets
MAP_UPDATE_INTERVAL equ 1 ; ticks
MAP_UPDATE_INTERVAL_MOVING equ 1 ; ticks, for moving targets

; bits
MAP_WALL equ 0x8000
MAP_BIOMASS equ 0x4000
MAP_IRON equ 0x0200
MAP_COPPER equ 0x0100
MAP_CUBOT equ 0x0080



;;                                                              0. START
;;  ********************************************************************

.data

debugValue: dw 0 ; printed value

; bot status
facingDir: dw -1 ; -1 means don't know yet



.text

entryPoint:
    ; run tests or main code
    mov a, RUN_TESTS
    cmp a, 0
    jz run
    jmp runAllTests



;;  runAllTests (entry point)

; Runs tests, shows results, and halts.

; Results marked by 0xF00D on success, 0xDEAD on failure.
; Next value is number of passed tests, more details may follow.

runAllTests:
    call _preAllTests
    

    ;;  --- TESTS ---  #tsts

    ;;  behavior

    ;;  drivers
    mov [nextTest], testClockModulo
    call runTest

    ;;  testing

    ;;  math
    mov [nextTest], testSeedRng
    call runTest
    mov [nextTest], testRandom
    call runTest
    mov [nextTest], testAbs1
    call runTest
    mov [nextTest], testAbs2
    call runTest
    mov [nextTest], testAbs3
    call runTest

    ;;  types

    ;;  utility
    mov [nextTest], testCount1
    call runTest
    mov [nextTest], testCount2
    call runTest
    mov [nextTest], testCount3
    call runTest
    mov [nextTest], testTernaryOp1
    call runTest
    mov [nextTest], testTernaryOp2
    call runTest


    ;;  --- TESTS THAT COST ENERGY ---

    mov a, SKIP_COSTLY_TESTS
    cmp a, 0
    jnz runAllTests_end

    ;;  drivers
    mov [nextTest], testCostlyUpdateMap
    call runTest

    
    ;;  --- END TESTS ---

runAllTests_end:
    call _postAllTests
    brk



;;  run (entry point)

; Main code. Halts when finished.

run:
    ; cheat
    mov a, -1
    hwi HW_BATTERY
    
    ; commands
    
    call updateMapPeriodically
    mov a, MAP_CUBOT
    call searchMap ; position or (-1,-1) in x,y
    call updateTargetStatus
    
    ; show target x,y
    mov [debugValue], x
    shl [debugValue], 8
    or [debugValue], y
    
    mov a, 0 ; distance, set to 0 to avoid battery usage (FIXME)
    call moveTo
    mov [_isStopped], FALSE ; reset
    
    ; if battery full, take a step
    call usedCharge ; in b
    cmp b, 0
    jnz run_endif
    mov a, LEGS_WALK ; walk
    mov b, [facingDir]
    hwi HW_LEGS
    
run_endif:
    ; debug printing
    call displayDebugValues
    brk



;;  displayDebugValues()
;;  corrupt a,b

; TODO function to add a value to an array

displayDebugValues:
    push bp
    mov bp, sp
    
    ; print marker
    ;push 0xC0DE ; marker
    
    ; print values
    push [debugValue]
    
    ; print battery charge used
    call usedCharge
    push b
    
    
    ; numValues = bp - sp
    mov a, bp
    sub a, sp
    
    call marquee
    
    pop bp
    ret



;;                                                           1. BEHAVIOR
;;  ********************************************************************

didMapUpdateThisTick: dw FALSE
_lastTargetX: dw -1
_lastTargetY: dw -1
_isTargetMoving: dw FALSE



;;  updateMapPeriodically()
;;  corrupt all

; Must be called every tick for didMapUpdateThisTick to be correct.

updateMapPeriodically:
    mov [didMapUpdateThisTick], FALSE
    
    ; use different intervals based on whether target is moving
    mov a, [_isTargetMoving]
    mov b, MAP_UPDATE_INTERVAL_MOVING
    mov c, MAP_UPDATE_INTERVAL
    call ternaryOp ; select one of the constants into a
    
    ; modulo clock for periodic updates
    mov d, a
    call clockModulo ; y = clock % d
    
    cmp y, 0 ; if modulo is 0, time to update
    jnz updateMapPeriodically_end
    
    call updateMap
    
updateMapPeriodically_end:
    ret



;;  updateMapIfBatteryFull()
;;  corrupt b

updateMapIfBatteryFull:
    call usedCharge
    cmp b, 0 ; if battery not full, skip update
    jnz updateMapIfBatteryFull_end
    
    call updateMap
    
updateMapIfBatteryFull_end:
    ret



;;  updateTargetStatus()
;;  in x,y: newTarget

; Sets _lastTargetX, _lastTargetY, _isTargetMoving.

updateTargetStatus:
    ; only update if map has updated
    cmp [didMapUpdateThisTick], 0
    jz updateTargetStatus_end
    
    ; has target moved?
    cmp [_lastTargetX], x
    jnz updateTargetStatus_moved
    cmp [_lastTargetY], y
    jnz updateTargetStatus_moved
    
    ; not moved
    mov [_isTargetMoving], FALSE
    ret
    
updateTargetStatus_moved:
    mov [_isTargetMoving], TRUE
    mov [_lastTargetX], x
    mov [_lastTargetY], y
    
updateTargetStatus_end:
    ret



;;                                                            2. DRIVERS
;;  ********************************************************************

;;                                                          Movement
;;  ----------------------------------------------------------------

_isStopped: dw FALSE ; disable further commands?



;;  stepOnce()
;;  in b: direction
;;  corrupt a

; Manual command to take one step.

stepOnce:
    cmp [_isStopped], TRUE ; if _isStopped, exit
    jz stepOnce_end
    
    mov a, LEGS_WALK
    hwi HW_LEGS ; direction already in b
    
    mov [_isStopped], TRUE ; prevent walking again
    
stepOnce_end:
    ret



;;  stepN()
;;  in a: numSteps
;;  in b: direction
;;  corrupt a,d

; Manual command to take multiple steps in one direction.

stepN_stepsLeft: dw -1 ; -1 means no stepN command started yet

stepN:
    cmp [_isStopped], TRUE ; if _isStopped, exit
    jz stepN_end
    
    mov d, a ; numSteps
    
    ; take step
    mov a, LEGS_WALK
    hwi HW_LEGS ; direction already in b
    
    ; if stepsLeft not set, set it
    cmp [stepN_stepsLeft], 0
    jge stepN_endif
    mov [stepN_stepsLeft], d ; numSteps
    
stepN_endif:
    ; decrement stepsLeft
    sub [stepN_stepsLeft], 1
    
    ; halt if finished
    cmp [stepN_stepsLeft], 0
    jg stepN_end
    mov [_isStopped], TRUE
    
stepN_end:
    ret



;;  faceToward()
;;  in x,y: target
;;  corrupt all

; Costs 20 kJ if need to turn.

faceToward:

    ; put offset from own pos in c,d
    mov c, x ; copy
    mov d, y
    mov a, LIDAR_GET_POS
    hwi HW_LIDAR
    sub c, x ; get offset
    sub d, y
    
    ; put absolute value of offset coords in x,y
    mov a, c
    call abs
    mov x, a
    mov a, d
    call abs
    mov y, a

    ; pick larger dimension
    cmp x, y ; if abs(x) > abs(y), face a horizontal direction, else vertical
    jl faceToward_vertical

faceToward_horizontal:
    mov b, DIR_E ; face east if positive x
    cmp c, 0 ; if negative x, flip to west
    jl faceToward_flip
    jmp faceToward_turn
    
faceToward_vertical:
    mov b, DIR_N ; face north if negative y
    cmp d, 0 ; if positive y, flip to south
    jg faceToward_flip
    jmp faceToward_turn
    
faceToward_flip:
    add b, 2 ; flip compass direction
    
faceToward_turn:
    ; don't turn if already facing
    cmp [facingDir], b
    jz faceToward_end
    
    mov a, LEGS_TURN
    hwi HW_LEGS ; direction already in b
    
    mov [facingDir], b ; save direction
    
faceToward_end:
    ret



;;  moveTo()
;;  in a: distance
;;  in x,y: destination
;;  corrupt all

; Manual command to pathfind and move to destination.
; Costs an extra 50 kJ per pathfind.
; Will not pathfind if destination already reached or outside bounds.

moveTo_pathPtr: dw -1 ; -1 means not started

moveTo:
    push a ; distance
    
    cmp [_isStopped], TRUE ; if _isStopped, exit
    jz moveTo_end
    
    ; initialize
    jmp moveTo_endif
    
    cmp [moveTo_pathPtr], -1 ; if already started on path, skip initialization
    jnz moveTo_walkPath
    
    ; check destination is valid
    test x, 0xfff0 ; if x or y are not in [0, 16), halt
    jnz moveTo_halt
    test y, 0xfff0
    jnz moveTo_halt
    
    ; check we're not already at destination
    mov c, x ; store x,y
    mov d, y
    mov a, LIDAR_GET_POS
    hwi HW_LIDAR
    
    cmp c, x ; if position == destination, halt
    jnz moveTo_endif
    cmp d, y
    jnz moveTo_endif
    jmp moveTo_halt
    
    ; end initialize
    
moveTo_endif:
    ; calculate path
    mov a, LIDAR_GET_PATH ; pathfind to x,y
    mov b, [sp+0] ; distance
    hwi HW_LIDAR
    
    ; start on path
    mov [moveTo_pathPtr], LIDAR_PATH_ADDR
    
moveTo_walkPath:
    ; read step
    mov b, [moveTo_pathPtr] ; read from path memory
    mov b, [b]
    cmp b, -1 ; halt if invalid
    jz moveTo_halt
    cmp b, 0xAAAA ; halt if reached end (marked by magic number)
    jz moveTo_halt
    
    ; take step
    mov a, LEGS_WALK
    hwi HW_LEGS ; direction is in b
    
    ; advance to next step
    add [moveTo_pathPtr], 1
    
    ; done
    jmp moveTo_end
    
moveTo_halt:
    ; halt
    mov [_isStopped], TRUE
    
moveTo_end:
    add sp, 1 ; pop distance
    ret



;;                                                           Display
;;  ----------------------------------------------------------------

;;  marquee(value...)
;;  in a: numValues

; Show values in sequence using hologram.
; Changing numValues will throw off the sequence.

marquee:
    push bp
    mov bp, sp
    push a
    push b
    push c
    push d
    push x
    push y
    
    mov d, a ; numValues
    
    ; calculate index
    hwi HW_CLOCK ; clock in b:c
    mov y, b ; copy to y:a
    mov a, c
    div d ; index in y = clock % numValues
    
    ; show indexed value
    mov b, d ; numValues
    sub b, y ; - index (cuz upside down stack)
    add b, bp ; + bp
    mov b, [b+1] ; + arg offset of 2 - 1 = &value
    mov a, HOLO_DISPLAY_HEX
    hwi HW_HOLO
    
    pop y
    pop x
    pop d
    pop c
    pop b
    pop a
    pop bp
    ret a ; pop args



;;                                                           Sensors
;;  ----------------------------------------------------------------

;;  updateMap()

; Copy map to memory at LIDAR_MAP_ADDR for 10 kJ.
; Sets didMapUpdateThisTick.

updateMap:
    push a

    ; get map
    mov a, LIDAR_GET_MAP
    hwi HW_LIDAR
    mov [didMapUpdateThisTick], TRUE

    pop a
    ret


testCostlyUpdateMap:
    call updateMap
    ret



;;  usedCharge()
;;  out b: capacity - charge

usedCharge:
    push a
    push c

    mov a, BATTERY_GET_CHARGE
    hwi HW_BATTERY
    mov c, b
    mov a, BATTERY_GET_CAPACITY
    hwi HW_BATTERY
    sub b, c

    pop c
    pop a
    ret



;;  clockModulo()
;;  in d: divisor
;;  out y: result

; Returns number of current tick, modulo divisor.

clockModulo:
    push a
    push b
    push c

    hwi HW_CLOCK ; time in b:c
    mov y, b ; copy b:c to y:a for modulo
    mov a, c
    div d ; clock % interval gives remainder in y

    pop c
    pop b
    pop a
    ret


testClockModulo:
    ; anything modulo 1 is 0
    ; easier to test than actual modulo
    mov d, 1

    mov [expectD], d
    mov [expectY], 0

    call clockModulo
    ret



;;                                                            3. TESTING
;;  ********************************************************************

; Test functions should be careful not to modify any registers themselves.
; The registers will be set to random values and checked for modification.

; Example assert usages:
;   mov [expectA], 0x1234
;   or [expectCorruptRegs], REG_A

; Example test run:
;   mov [nextTest], testMyStuff
;   call runTest


; the test func, i.e. a func whose name ends with "_test"
; which in turn calls the *tested* func(s)
nextTest: dw 0 ; addr

; which registers should we NOT check?
expectCorruptRegs: dw 0x0000 ; flags

; asserted values
expectA: dw 0
expectB: dw 0
expectC: dw 0
expectD: dw 0
expectX: dw 0
expectY: dw 0
expectBP: dw 0
expectSP: dw 0

; which registers failed their checks?
_checkFailedRegs: dw 0x0000 ; flags

; for error reporting
_actualA: dw 0
_actualB: dw 0
_actualC: dw 0
_actualD: dw 0
_actualX: dw 0
_actualY: dw 0
_actualBP: dw 0
_actualSP: dw 0

; set to prevent running tests more than once
_testsPerformed: dw FALSE

; helps identify which test failed
_numTestsPassed: dw 0



_preAllTests:
    ; if tests finished, show results
    cmp [_testsPerformed], TRUE
    jz _preAllTests_results
    mov [_testsPerformed], TRUE
    
    ; init RNG
    call seedRng
    
    ret
    
_preAllTests_results:
    call _showTestResults
    brk



_postAllTests:
    call _showTestResults
    ret



_showTestResults:
    ; if all passed, show success
    cmp [_checkFailedRegs], 0x0000
    jz _showTestResults_success
    
    ; show failure
    push 0xDEAD ; marker
    push [_numTestsPassed]
    push [_checkFailedRegs]
    push [_actualA]
    push [_actualB]
    push [_actualC]
    push [_actualD]
    push [_actualX]
    push [_actualY]
    mov a, 9 ; numValues
    call marquee
    
    jmp _showTestResults_end
    
_showTestResults_success:
    ; show success
    push 0xF00D ; marker
    push [_numTestsPassed]
    mov a, 2 ; numValues
    call marquee
    
_showTestResults_end:
    ret



;;  runTest()
;;  in a: testFunc
;;  corrupt all

; Runs testFunc with pre and post tasks.

runTest:
    call _preTest
    call [nextTest]
    call _postTest
    mov [nextTest], 0 ; reset
    ret


_preTest:
    mov [expectCorruptRegs], 0x0000

    ; initialize registers with random numbers (except bp, sp)
    call random
    mov [expectA], a
    call random
    mov [expectB], a
    call random
    mov [expectC], a
    call random
    mov [expectD], a
    call random
    mov [expectX], a
    call random
    mov [expectY], a
    mov [expectBP], bp
    mov [expectSP], sp ; sp last

    mov a, [expectA]
    mov b, [expectB]
    mov c, [expectC]
    mov d, [expectD]
    mov x, [expectX]
    mov y, [expectY]

    ret


_postTest:
    ; save all registers
    mov [_actualA], a
    mov [_actualB], b
    mov [_actualC], c
    mov [_actualD], d
    mov [_actualX], x
    mov [_actualY], y
    mov [_actualBP], bp
    mov [_actualSP], sp

    ; check that registers match expected values

    mov [_checkFailedRegs], 0x0000 ; clear errors
    mov c, [expectCorruptRegs]

    ; check register A
    test c, REG_A ; if expected corrupt, pass
    jnz _postTest_endA
    cmp [_actualA], [expectA] ; if actual == expected, pass
    jz _postTest_endA
    or [_checkFailedRegs], REG_A ; fail
_postTest_endA:
    ; check register B...
    test c, REG_B
    jnz _postTest_endB
    cmp [_actualB], [expectB]
    jz _postTest_endB
    or [_checkFailedRegs], REG_B
_postTest_endB:
    test c, REG_C
    jnz _postTest_endC
    cmp [_actualC], [expectC]
    jz _postTest_endC
    or [_checkFailedRegs], REG_C
_postTest_endC:
    test c, REG_D
    jnz _postTest_endD
    cmp [_actualD], [expectD]
    jz _postTest_endD
    or [_checkFailedRegs], REG_D
_postTest_endD:
    test c, REG_X
    jnz _postTest_endX
    cmp [_actualX], [expectX]
    jz _postTest_endX
    or [_checkFailedRegs], REG_X
_postTest_endX:
    test c, REG_Y
    jnz _postTest_endY
    cmp [_actualY], [expectY]
    jz _postTest_endY
    or [_checkFailedRegs], REG_Y
_postTest_endY:
    test c, REG_BP
    jnz _postTest_endBP
    cmp [_actualBP], [expectBP]
    jz _postTest_endBP
    or [_checkFailedRegs], REG_BP
_postTest_endBP:
    test c, REG_SP
    jnz _postTest_endSP
    cmp [_actualSP], [expectSP]
    jz _postTest_endSP
    or [_checkFailedRegs], REG_SP
_postTest_endSP:

    ; if all passed, move on
    cmp [_checkFailedRegs], 0x0000
    jz _postTest_end

    ; else show error and halt
    call _showTestResults
    brk ; halt

_postTest_end:
    add [_numTestsPassed], 1 ; won't reach this if failed
    ret



;;                                                               4. MATH
;;  ********************************************************************

_rngState: dw 0x5837 ; randomly chosen initial value



;;  seedRng()

; Seed RNG using hardware RNG, at small energy cost.

seedRng:
    push b
    hwi HW_RNG
    mov [_rngState], b
    pop b
    ret


testSeedRng:
    call seedRng
    ret



;;  random()
;;  out a: result

; Returns random word without relying on hardware RNG.
; http://www.retroprogramming.com/2017/07/xorshift-pseudorandom-numbers-in-z80.html

random:
    push b
    mov a, [_rngState]
    mov b, a ; xs ^= xs << 7
    shl b, 7
    xor a, b
    mov b, a ; xs ^= xs >> 9
    shr b, 9
    xor a, b
    mov b, a ; xs ^= xs << 8
    shl b, 8
    xor a, b
    mov [_rngState], a
    pop b
    ret


testRandom:
    or [expectCorruptRegs], REG_A ; result is random, can't predict it
    call random
    ret



;;  abs()
;;  in a: value
;;  out a: absoluteValue

abs:
    push b

    ; some clever algorithm
    mov b, a ; copy
    sar b, 15 ; fill with sign bit
    xor a, b ; if negative, flip all bits
    sub a, b ; if negative, add 1 (twos-complement)

    pop b
    ret


testAbs1: ; abs(0) -> 0
    mov a, 0
    mov [expectA], 0
    call abs
    ret
testAbs2: ; abs(11) -> 11
    mov a, 11
    mov [expectA], 11
    call abs
    ret
testAbs3: ; abs(-11) -> 11
    mov a, -11
    mov [expectA], 11
    call abs
    ret



;;                                                              5. TYPES
;;  ********************************************************************

;;                                                               Map
;;  ----------------------------------------------------------------

;;  searchMap()
;;  in a: item to search for
;;  out x,y: position or (-1,-1)
;;  corrupt all

; Searches the map that's in memory, so make sure it's up to date.
; Ignores own position.

searchMap:
    push a ; item
    
    ; index of own position, so we can skip it
    ; (in case we're searching for cubots)
    mov a, LIDAR_GET_POS
    hwi HW_LIDAR
    call mapXYToIndex
    mov b, a ; ownIndex
    add b, LIDAR_MAP_ADDR
    
    ; search map for item
    pop a ; value = item
    mov c, LIDAR_MAP_ADDR ; startAddr
    mov d, c
    add d, MAP_SIZE ; endAddr
    
searchMap_loop:
    ; search
    call findNext ; out c
    
    ; if found own position, keep searching
    cmp c, b
    jnz searchMap_finishedSearch
    add c, 1 ; skip this position
    jmp searchMap_loop

searchMap_finishedSearch:
    cmp c, d ; if not found, return (-1, -1)
    jz searchMap_fail
    
    ; index = resultAddr - startAddr
    mov a, c
    sub a, LIDAR_MAP_ADDR
    
    ; return x,y
    call mapIndexToXY
    jmp searchMap_end
    
searchMap_fail:
    mov x, -1
    mov y, -1

searchMap_end:
    ret



;;  mapXYToIndex()
;;  in x,y: coords
;;  out a: index in [0, MAP_SIZE), i.e. a byte

mapXYToIndex:
    ; index bits are 0000 0000 yyyy xxxx
    mov a, y
    shl a, 4
    or a, x
    ret



;;  mapIndexToXY()
;;  in a: index in [0, MAP_SIZE), i.e. a byte
;;  out x,y: coords

mapIndexToXY:
    ; index bits are 0000 0000 yyyy xxxx
    mov x, a
    and x, 0x000f
    
    mov y, a
    shr y, 4
    and y, 0x000f
    ret



;;                                                            6. UTILITY
;;  ********************************************************************

;;  findNth()
;;  in a: value
;;  in b: N
;;  in c: startAddr
;;  in d: endAddr
;;  out c: result or endAddr
;;  corrupt b

; Search memory sequentially for Nth occurrence of value,
; where the first occurrence is the 0th.
; Return address of Nth occurrence, or endAddr if not found.
; endAddr is one past last item to be searched.

findNth:
    ; start loop
    cmp c, d ; if reached end, return
    jge findNth_end
    
    call findNext ; find next occurrence
    
    cmp b, 0 ; if searched N times, return
    jz findNth_end
    
    sub b, 1 ; advance
    jmp findNth ; loop
    
findNth_end:
    ret



;;  findNext()
;;  in a: value
;;  in c: startAddr
;;  in d: endAddr
;;  out c: result or endAddr

; Search memory sequentially for value.
; Return address of first occurrence, or endAddr if not found.
; endAddr is one past last item to be searched.

findNext:
    ; start loop
    cmp c, d ; if reached end, return
    jge findNext_end
    
    cmp a, [c] ; if found value, return
    jz findNext_end
    
    add c, 1 ; advance
    jmp findNext ; loop
    
findNext_end:
    ret



;;  count()
;;  in a: value
;;  in c: startAddr
;;  in d: endAddr
;;  out b: count

; Search memory region for value, count number of occurrences.
; endAddr is one past last item to be searched.

count:
    push c
    mov b, 0 ; counter
    
count_loop:
    ; start loop
    cmp c, d ; if reached end, return
    jge count_end
    
    cmp a, [c] ; if found value, increment counter
    jnz count_next
    add b, 1
    
count_next:
    add c, 1 ; advance
    jmp count_loop ; loop
    
count_end:
    pop c
    ret


testCount1: ; 11 shows up 2 times
    mov a, 11
    mov [expectB], 2
    jmp testCount_helper
testCount2: ; 22 shows up 3 times
    mov a, 22
    mov [expectB], 3
    jmp testCount_helper
testCount3: ; 33 shows up 0 times
    mov a, 33
    mov [expectB], 0
    jmp testCount_helper

dw 11 ; don't count before start
testCount_data:
dw 11, 22, 22, 22, 11 ; length 5
dw 22 ; don't count after end

testCount_helper:
    mov c, testCount_data ; startAddr
    mov d, testCount_data ; endAddr = startAddr + length
    add d, 5 ; length
    
    mov [expectA], a
    mov [expectC], c
    mov [expectD], d

    call count
    ret



;;  ternaryOp
;;  in a: TRUE or FALSE
;;  in b: valueIfTrue
;;  in c: valueIfFalse
;;  out a: result
;;  corrupt b,c

; Pick one of two values based on boolean.
; In other words: a = (a ? b : c)

ternaryOp:
    and b, a
    not a
    and a, c
    or a, b
    ret


testTernaryOp1:
    mov a, TRUE
    mov b, 11
    mov c, 22

    mov [expectA], 11
    or [expectCorruptRegs], REG_B
    or [expectCorruptRegs], REG_C
    
    call ternaryOp
    ret

testTernaryOp2:
    mov a, FALSE
    mov b, 11
    mov c, 22

    mov [expectA], 22
    or [expectCorruptRegs], REG_B
    or [expectCorruptRegs], REG_C
    
    call ternaryOp
    ret




